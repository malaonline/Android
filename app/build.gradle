apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            try {
                storeFile file(KEYSTORE_FILE_PATH1)
                storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : System.getenv("KEYSTORE_PASSWORD");
                keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME");
                keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : System.getenv("KEY_PASSWORD");
            }
            catch (ex) {
                throw new InvalidUserDataException(
                        "You should define KEYSTORE_FILE_PATH, KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }

        debug {
            try {
                storeFile file(KEYSTORE_FILE_PATH1)
                storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : System.getenv("KEYSTORE_PASSWORD");
                keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME");
                keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : System.getenv("KEY_PASSWORD");
            }
            catch (ex) {
                throw new InvalidUserDataException(
                        "You should define KEYSTORE_FILE_PATH, KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }

    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        applicationId "com.malalaoshi.android"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 19
        versionName "3.4.0"

        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            manifestPlaceholders = [jpush_key: PARENT_JPUSH_KEY_DEBUG]
            buildConfigField("String", "API_HOST", "\"" + DEV_SERVER + "\"")
            resValue("string", "api_host", "\"" + DEV_SERVER + "\"")
            resValue("string", "build_type", "debug")
        }
        devrelease {
            applicationIdSuffix ".dev"
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [jpush_key: PARENT_JPUSH_KEY_DEV]
            buildConfigField("String", "API_HOST", "\"" + DEV_SERVER + "\"")
            resValue("string", "api_host", "\"" + DEV_SERVER + "\"")
            resValue("string", "build_type", "release")
        }
        stagerelease {
            applicationIdSuffix ".stage"
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [jpush_key: PARENT_JPUSH_KEY_STAGE]
            buildConfigField("String", "API_HOST", "\"" + STAGE_SERVER + "\"")
            resValue("string", "api_host", "\"" + STAGE_SERVER + "\"")
            resValue("string", "build_type", "release")
        }
        prdrelease {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [jpush_key: PARENT_JPUSH_KEY_RELEASE]
            buildConfigField("String", "API_HOST", "\"" + PRD_SERVER + "\"")
            resValue("string", "api_host", "\"" + PRD_SERVER + "\"")
            resValue("string", "build_type", "release")
        }
    }
    variantFilter { // buildTypes没有release
        if (it.buildType.name == 'release') {
            it.setIgnore(true)
        }
    }

    lintOptions {
        abortOnError true
    }

    productFlavors {
        /*teacher {
            applicationId "com.malalaoshi.android.teacher"
        }*/
        parent {
            applicationId "com.malalaoshi.android.parent"
        }
    }

    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    project.afterEvaluate {
        //重置ShareSDK中appId和appSecret
        tasks.getByName('generateParentDevreleaseAssets') {
            it.doFirst {
                initShareSdk(PARENT_APP_ID_DEV,PARENT_APP_SECRET_DEV)
            }
        }
        tasks.getByName('generateParentStagereleaseAssets') {
            it.doFirst {
                initShareSdk(PARENT_APP_ID_STAGE,PARENT_APP_SECRET_STAGE)
            }
        }
        tasks.getByName('generateParentPrdreleaseAssets') {
            it.doFirst {
                initShareSdk(PARENT_APP_ID_RELEASE,PARENT_APP_SECRET_RELEASE)
            }
        }
    }
    dexOptions{
        javaMaxHeapSize "4g"
    }
}

def initShareSdk(appId, appSecret) {
    def DevInfor = new XmlParser().parse(file("src/main/ShareSDK.demo.xml"))
    DevInfor.ShareSDK.@AppKey = PARENT_SHARESDK_APP_KEY
    DevInfor.Wechat.@AppId = appId
    DevInfor.Wechat.@AppSecret = appSecret
    DevInfor.WechatMoments.@AppId = appId
    DevInfor.WechatMoments.@AppSecret = appSecret

    file("src/main/assets/").mkdirs()
    file("src/main/assets/ShareSDK.xml").withPrintWriter { printWriter ->
        XmlNodePrinter nodePrinter = new XmlNodePrinter(printWriter);
        nodePrinter.setPreserveWhitespace(true);
        printWriter.println('<?xml version="1.0" encoding="utf-8"?>')
        nodePrinter.print(DevInfor);
        printWriter.flush()
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.5'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.5'
    compile 'com.nineoldandroids:library:2.4.0'
    compile project(':pingpp')
    compile project(':pingpp_one')
    compile project(':core')
    compile project(':ui')
    compile project(':report')
    compile project(':qr')
    compile 'me.relex:circleindicator:1.2.1'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:multidex:1.0.1'
    compile files('libs/MobCommons-2016.1020.1028.jar')
    compile files('libs/MobTools-2016.1020.1028.jar')
    compile files('libs/ShareSDK-Core-2.7.8.jar')
    compile files('libs/ShareSDK-Wechat-2.7.8.jar')
    compile files('libs/ShareSDK-Wechat-Core-2.7.8.jar')
    compile files('libs/ShareSDK-Wechat-Moments-2.7.8.jar')
}
